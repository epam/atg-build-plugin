/*
 * Copyright 2019 EPAM SYSTEMS INC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compile gradleApi()
    compile localGroovy()
}

repositories {
    mavenCentral()
}

targetCompatibility = JavaVersion.VERSION_1_6

jar {
    manifest {
        attributes 'Implementation-Title': "${rootProject.name}",
                'Implementation-Version': version,
                'Implementation-Vendor': 'EPAM SYSTEMS INC'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                packaging 'jar'
                name 'atg-build-plugin'
                description 'ATG Build Plugin for gradle'
                url 'https://github.com/epam/atg-build-plugin'

                scm {
                    url 'https://github.com/epam/atg-build-plugin'
                    connection 'scm:https://github.com/epam/atg-build-plugin.git'
                    developerConnection 'scm:https://github.com/epam/atg-build-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'dmitrykolin'
                        name 'Dzmitry Kolin'
                        email 'kolin.dmitry@gmail.com'
                    }
                    developer {
                        id 'prokofiev'
                        name 'Aleksei Prokofev'
                        email 'prokofiev.ad@yandex.ru'
                    }
                    developer {
                        id 'shvedov'
                        name 'Vadim Shvedov'
                        email 'lungesoft@gmail.com'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}